run_zsh_setup() {
    # scenarios
    # - connected via local tty: run/use ssh agent
    # - connected remotely: start tmux, use remote agent and fallback to local
    # - running in container: use bound agent
    if ! ssh_agent_configured
    then
        echo "no ssh agent configured"
        if can_run_ssh_agent && ! local_ssh_agent_running
        then
            start_ssh_agent
        fi
        if local_ssh_agent_running
        then
            eval "$(<~/.ssh-agent-thing)" > /dev/null
        fi
    fi
    # Run ssh agent if not in an ssh session
    if currently_in_ssh_tty; then
        echo "in ssh tty"
        setup_ssh_session
        if test -z $ZSH_EXECUTION_STRING; then
            launch_tmux
        fi
    fi
}

local_ssh_agent_running() {
    pgrep -u "$USER" ssh-agent > /dev/null
}

ssh_agent_configured() {
    test -n "$SSH_AUTH_SOCK"
}

can_run_ssh_agent() {
    [[ -f $HOME/.ssh/id_rsa || -f $HOME/.ssh/id_ed25519 ]]
}

start_ssh_agent() {
    echo "starting ssh agent"
    ssh-agent > ~/.ssh-agent-thing
    #eval "$(<~/.ssh-agent-thing)" > /dev/null
}

currently_in_ssh_tty() {
    test -n "$SSH_TTY"
}

currently_in_ssh_session() {
    if currently_in_ssh_tty; then
        return 0
    elif test -n "$TMUX_PANE"; then
        base_tty_of_tmux_session=$(tmux list-clients -t ${TMUX_PANE:1} -F '#{client_tty}')
        if who | grep "${base_tty_of_tmux_session:5} " > /dev/null; then
            return 0
        fi
    fi
    return 1
}

setup_ssh_session() {
    if test -n "$DISPLAY"
    then
        echo "caching display variable"
        echo $DISPLAY > ~/.display.txt
    fi
    if ssh_agent_configured
    then
        echo "caching ssh agent socket"
        echo "SSH_AUTH_SOCK=$SSH_AUTH_SOCK; export SSH_AUTH_SOCK;" > ~/.ssh-agent-thing-remote
        export WANTS_REMOTE_SSH_AGENT='true'
    fi
}

launch_tmux() {
    # if not already inside a tmux session...
    if test -z "$TMUX"; then
        echo "launch tmux"
        # attach to any unused sessions, or start a new session
        # tmux ls
        # echo "get session"
        session=$(tmux ls | grep -v attached | head -1 | cut -f1 -d:)
        if [[ $session == "" ]]; then
            # echo "new $TERM"
            tmux new-session
        else
            # echo "attach: $session"
            tmux attach -t $session
        fi
   fi
   # when quitting tmux, quit totally
   while test -z ${TMUX}; do
       exit
   done
}

check_dotfiles() {
    if [ -d $HOME/dotfiles ]
    then
        check=$(cd $HOME/dotfiles && git fetch --dry-run)
        if [[ $check != "" ]]
        then
            echo "changes detected in dotfiles:\n$check"
            echo "to update run: cd $HOME/dotfiles && git pull"
        fi
    fi
}

run_zsh_setup
(check_dotfiles &)

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi
if [[ -r "/usr/share/zsh-theme-powerlevel10k/powerlevel10k.zsh-theme" ]]; then
    source "/usr/share/zsh-theme-powerlevel10k/powerlevel10k.zsh-theme"
else
    source "${HOME}/.powerlevel10k/powerlevel10k.zsh-theme"
fi
# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# Automatic zsh configuration
zstyle ':completion:*' completer _expand _complete _ignored _approximate
zstyle ':completion:*' completions 1
zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' glob 1
zstyle ':completion:*' insert-unambiguous true
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' matcher-list '' '+m:{[:lower:]}={[:upper:]} m:{[:lower:][:upper:]}={[:upper:][:lower:]}' '+r:|[._-]=** r:|=** l:|=*'
zstyle ':completion:*' max-errors 3
zstyle ':completion:*' menu select=long
zstyle ':completion:*' original true
zstyle ':completion:*' preserve-prefix '//[^/]##/'
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' substitute 1
zstyle ':completion:*' verbose true
zstyle :compinstall filename "$HOME/.zshrc"
autoload -Uz compinit
compinit

# Source system specific settings
source ~/.zprofile

HISTFILE=~/.histfile
HISTSIZE=16384
SAVEHIST=262144
setopt hist_ignore_dups hist_find_no_dups hist_ignore_space
setopt sharehistory appendhistory extendedhistory autocd extendedglob nomatch notify prompt_subst
setopt correct
bindkey -v

autoload -U colors && colors

# Vim-like bindings
bindkey "^R" history-incremental-search-backward
bindkey -a "/" history-incremental-search-backward
bindkey -v "^[[8~" end-of-line
bindkey -a "^[[8~" vi-end-of-line
bindkey -v "^[[7~" beginning-of-line
bindkey -a "^[[7~" vi-beginning-of-line
# helix-like
bindkey -a "d" vi-delete-char
bindkey -a "x" visual-line-mode
bindkey -M visual "d" vi-delete
# todo: try using `bindkey -N move visual` to create a new "move" keymap to be distinct from the normal visual map
command-forward-word() {
    zle visual-mode
    zle -U s
    zle vi-set-mark
    zle vi-forward-word
    zle vi-backward-char
    zle -U e
    zle vi-set-mark
}
command-back-word() {
    zle visual-mode
    zle -U e
    zle vi-set-mark
    zle vi-backward-word
    zle -U s
    zle vi-set-mark
}
visual-forward-word() { zle visual-mode; zle vi-forward-char; command-forward-word}
visual-back-word() { zle visual-mode; zle vi-backward-char; command-back-word}
insert-word-start() { zle -U s; zle vi-goto-mark; zle vi-insert }
append-word-end() { zle -U e; zle vi-goto-mark; zle vi-add-next }
zle -N command-forward-word
zle -N command-back-word
zle -N visual-forward-word
zle -N visual-back-word
zle -N insert-word-start
zle -N append-word-end
bindkey -a "o" command-mark-a
bindkey -a "w" command-forward-word
bindkey -M visual "w" visual-forward-word
bindkey -a "b" command-back-word
bindkey -M visual "b" visual-back-word
bindkey -M visual "i" insert-word-start
bindkey -M visual "a" append-word-end

source <(fzf --zsh)
export FZF_CTRL_R_OPTS="--wrap"


export EDITOR="vim"
export PATH="$HOME/.local/bin:$PATH"

alias :q="exit"
alias ls='ls --color'
alias ll='ls --color -lah'
# alias vi='vim'
alias rm='rm -i'
alias grep='grep --color'

hx() {
    if [ -d ~/src/helix-image ]
    then
        (cd ~/src/helix-image; git pull >/dev/null &)
    else
        git clone --depth 1 git@git-collab.nrc-cnrc.gc.ca:haa/draosw/proto/helix-image.git ~/src/helix-image
    fi
    ~/src/helix-image/run.sh ${@[@]}
}

vi() {
    poetry_path=$(poetry env info -p)
    if [[ $? == 0 ]]
    then
        PYTHONPATH=$poetry_path/lib/python3.11/site-packages:$PYTHONPATH vim -p $@
    else
        vim -p $@
    fi
}

spin() {
    set +o monitor
    cmd=$1
    spin='⠋⠙⠚⠞⠖⠦⠴⠲⠳⠓'
    #spin='/-\|'
    i=0
    eval $cmd >/dev/null &
    pid=$!
    while kill -0 $pid &>/dev/null; do
        i=$(((i + 1) % 10))
        printf "$fg[magenta]\r${spin:$i:1}"
        printf "$fg[white] $2"
        sleep .1
    done
    wait $pid
    code=$?
    if [[ $code == 0 ]] then
        mark="$fg[green]✓"
    else
        mark="$fg[red]✗"
    fi
    printf "\r$mark\n$fg[white]"
    return $code
}

gcred() {
    pw=$(gopass websites/git-collab.nrc-cnrc.gc.ca/dustin.lagoy read-only)
    export cred="protocol=https
host=git-collab.nrc-cnrc.gc.ca
username=dustin.lagoy
password=$pw"
    echo $cred | git credential approve
    unset cred
    unset pw
}

dbuild() {
    export DOCKER_BUILDKIT=1
    pw=$(gopass websites/git-collab.nrc-cnrc.gc.ca/dustin.lagoy read-only)
    export cred="protocol=https
host=git-collab.nrc-cnrc.gc.ca
username=dustin.lagoy
password=$pw"
    name=$(basename $PWD)
    build_cmd="docker build --secret id=git_credentials,env=cred -t $name $@"
    eval $build_cmd
    unset cred
}

drun() {
    name=$(basename $PWD)
    docker run --rm -it -v $PWD:/work -w /work $name $@
}

pbuild() {
    name=$(basename $PWD)
    build_cmd="podman build --secret id=git_credentials,src=/home/lagoyd/cred -t $name ."
    if [[ $1 == "-v" ]] then
        eval $build_cmd
        shift
    elif [[ $1 == "-q" ]] then
        eval $build_cmd >/dev/null
        shift
    else
        spin $build_cmd "building container $name"
    fi
}

prun() {
    name=$(basename $PWD)
    podman run --rm -it -v $PWD:/src/$name -w /src/$name $name $@
}

history(){
    command sed 's/^. [0-9]*...//' < $HOME/.histfile | grep -i --color "$@"
}
alias hist='history'

precmd() {
    if currently_in_ssh_session
    then
        if test -s ~/.display.txt
        then
            export DISPLAY=$(cat ~/.display.txt)
        fi
        if [[ $WANTS_REMOTE_SSH_AGENT == 'true' ]]
        then
            eval "$(<~/.ssh-agent-thing-remote)" > /dev/null
        fi
    fi
}
